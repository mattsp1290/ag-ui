pluginManagement {
    repositories {
        google()
        gradlePluginPortal()
        mavenCentral()
    }

    plugins {
        def kotlinVersion = "2.1.20"
        def agpVersion = "8.12.0"

        id("org.jetbrains.kotlin.android") version kotlinVersion
        id("org.jetbrains.kotlin.multiplatform") version kotlinVersion
        id("org.jetbrains.kotlin.plugin.serialization") version kotlinVersion
        id("com.android.application") version agpVersion
        id("com.android.library") version agpVersion
    }
}

rootProject.name = "agui-chatapp-java"

include(":app")
include(":chatapp-shared")
project(":chatapp-shared").projectDir = file("../chatapp-shared")

// Include local library for development (using local modules with new Activity types)
includeBuild("../../library") {
    dependencySubstitution {
        substitute(module("com.ag-ui.community:kotlin-core")).using(project(":kotlin-core"))
        substitute(module("com.ag-ui.community:kotlin-client")).using(project(":kotlin-client"))
        substitute(module("com.ag-ui.community:kotlin-tools")).using(project(":kotlin-tools"))
        // kotlin-a2ui is fetched from Maven (com.contextable:kotlin-a2ui)
    }
}
